plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

def buildProperties = new Properties()

rootDir.listFiles().each { file ->
    if (file.name == "build.properties") {
        buildProperties.load(file.newReader())
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    if (rootProject.include_extra_dependencies == true) {
        modImplementation "com.terraformersmc:modmenu:${rootProject.modmenu_version}"

        if (buildProperties.MC_VER > buildProperties.MC_1_19_2) {
            modApi("me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_version}") {
                if (buildProperties.MC_VER < buildProperties.MC_1_20_6) {
                    // If version is below 1.20.6 then we exclude fabric-api.
                    // This is because modmenu requires fabric-api for 1.20.6 and above.
                    if (buildProperties.MC_VER >= buildProperties.MC_1_21_8) {
                        // For some reason the fabric api does not get included. This is a quick fix.
                        exclude(group: "net.fabricmc.fabric-api")
                    }
                }
            }
        } else {
            modImplementation "me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_version}"
        }
    } else {
        modCompileOnlyApi "com.terraformersmc:modmenu:${rootProject.modmenu_version}"
        modCompileOnlyApi "me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_version}"
    }

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property "version", rootProject.mod_version
    inputs.property "compatible_versions", rootProject.compatible_versions_fabric_format

    filesMatching("fabric.mod.json") {
        expand "version": rootProject.mod_version, "compatible_versions": rootProject.compatible_versions_fabric_format
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xplugin:Manifold']
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
